plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

ext {
    libraryVersionName = "1.1.1"
}

android {
    compileSdkVersion 30

    resourcePrefix 'npc_'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFile 'consumer-proguard-rules.pro'
    }

    buildTypes {
        debug {
            matchingFallbacks = 'release'
        }
    }
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    api 'androidx.appcompat:appcompat:1.3.1'
}

task androidSourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

task androidJavadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier 'javadoc'
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.h6ah4i.android.widget'
                artifactId = 'numberpickercompat'
                version = libraryVersionName

                artifact androidSourceJar
                artifact androidJavadocJar

                pom {
                    name = 'NumberPickerCompat'
                    description = 'Backport Material Design NumberPicker for API level 14+ devices'
                    url = 'https://github.com/h6ah4i/android-numberpickercompat'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'h6ah4i'
                            name = 'Haruki Hasegawa'
                        }
                    }
                    scm {
                        connection = 'git:https://github.com/h6ah4i/android-numberpickercompat.git'
                        developerConnection = 'git:git@github.com:h6ah4i/android-numberpickercompat.git'
                        url = 'https://github.com/h6ah4i/android-numberpickercompat'
                    }
                }
            }

            debug(MavenPublication) {
                from components.debug

                groupId = 'com.h6ah4i.android.widget'
                artifactId = 'numberpickercompat-debug'
                version = libraryVersionName
            }
        }
        repositories {
            maven {
                name = 'BuildDir'

                def releasesRepoUrl = "${rootProject.layout.projectDirectory}/repo/releases"
                def snapshotsRepoUrl = "${rootProject.layout.projectDirectory}/repo/snapshots"
                url = libraryVersionName.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }

            maven {
                name = 'MavenCentral'

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = libraryVersionName.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    if (project.hasProperty('mavenCentralUsername') && project.hasProperty("mavenCentralPassword")) {
                        username = "$mavenCentralUsername"
                        password = "$mavenCentralPassword"
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.release
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}
